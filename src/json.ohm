JSON {
  Exp = Value

  ObjectOpen = "{"
  ObjectClose = "}"
  ArrayOpen = "["
  ArrayClose = "]"
  PairSeparator = ":"

  Value =
    Object
    | Array
    | String
    | Number
    | True
    | False
    | Null

  Object =
    ObjectOpen ListOf<Pair, ","> ObjectClose

  Pair =
    String PairSeparator Value

  Array =
    ArrayOpen ListOf<Value, ","> ArrayClose

  String =
    "\"" doubleStringCharacter* "\""

  doubleStringCharacter =
    ~("\"" | "\\") any -- nonEscaped
    | "\\" escapeSequence -- escaped

  escapeSequence =
    "\"" -- doubleQuote
    | "\\" -- reverseSolidus
    | "/" -- solidus
    | "b" -- backspace
    | "f" -- formfeed
    | "n" -- newline
    | "r" -- carriageReturn
    | "t" -- horizontalTab
    | "u" hexDigit hexDigit hexDigit hexDigit -- codePoint

  Number =
    numberLiteral

  numberLiteral =
    decimal exponent -- withExponent
    | decimal -- withoutExponent

  decimal =
    wholeNumber "." digit+ -- withFract
    | wholeNumber -- withoutFract

  wholeNumber =
    "-" unsignedWholeNumber -- negative
    | unsignedWholeNumber -- nonNegative

  unsignedWholeNumber =
    "0" -- zero
    | nonZeroDigit digit* -- nonZero

  nonZeroDigit = "1".."9"

  exponent =
    exponentMark ("+"|"-") digit+ -- signed
    | exponentMark digit+ -- unsigned

  exponentMark = "e" | "E"

  True = "true"
  False = "false"
  Null = "null"
}